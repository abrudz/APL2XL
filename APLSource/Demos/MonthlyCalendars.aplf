 MonthlyCalendars←{
     ⍝. Output 13 worksheets in a single workbook
     ⍝. Worksheet 1 contains all 12 calendar months
     ⍝. Worksheets 2-13 are named by the month, and contain a single months' calendar

     ⍝⍝⍝: Constants
     names←{⊂⍵}¨'jan' 'feb' 'mar' 'apr' 'may' 'jun' 'jul' 'aug' 'sep' 'oct' 'nov' 'dec'
     days←{∊⍵'day'}¨'sun' 'mon' 'tues' 'wednes' 'thurs' 'fri' 'satur'
     mdays←31 28 31 30 31 30 31 31 30 31 30 31

     Setup←{
         nmonths←≢mdays
         rpm←9 ⍝ rows occupied by each month/day/calendar grouping + 1
         alldays←⊂1+⍳¨mdays
         calMonth←{
             (lastM currentM nextM)←⍺[12|¯1 0 1+⍵] ⍝ 3 month window relative to the current month
             off←⍵⌷7|+\0,¯1↓mdays                ⍝ offset based on the starting weekday of the month
             trailing←((14-2)-off)↑nextM        ⍝ 14 because we need a maximum of 2 weeks trailing
             leading←((-off)-2)↑lastM           ⍝ (+,-)2  because 2019's calendar starts on tuesday
             6 7⍴leading,currentM,trailing      ⍝ append the leading months days with the current month, filling with the trailing days
         }
         calendar←alldays calMonth¨⍳nmonths
         cellPos←↓(⍪rpm×⍳nmonths),0
          ⍝. Starting cell addresses for month names, day names, and the calendar
         cellPos calendar
     }

     Export←{
         (cellPos calendar)←⍵
         range←{(r←⎕NS'').(Address Value)←⍵ ⍺ ⋄ r}
         sheet←{(s←⎕NS'').(Name Ranges)←⍺ ⍵ ⋄ s}
         (mlocs dlocs clocs)←{##.Main.XL.itc¨(⊂⍵)+¨cellPos}¨(0 0)(1 0)(2 0)

          ⍝. Create a sheet containing month, column days, and a 6x7 calendar for each month along the first column
         ranges←∊names calendar((≢dlocs)⍴⊂days)range¨¨mlocs clocs dlocs
         yearSheet←'Calendar'sheet ranges

          ⍝. In this case, we generate a sheet per month
          ⍝. pair each name with each calendar, and generate a sheetname per name, and 2 ranges per sheetname
          ⍝. Each sheet contains a weekday name range and a calendar range
         monthSheets←calendar{⍵ sheet(∊days ⍺ range¨'A1' 'A2')}¨names
         (wb←⎕NS'').(FileName Sheets)←((739⌶0),'/calendar')(∊yearSheet monthSheets)
         ##.Main.Export wb
     }

     Export Setup''
 }
