 TestStyleAdd←{
   ⍝. This function tests adding styles in all its various forms
   ⍝. When a unique combination of styles are added to a worksheet, first each unique item is added to their respective tables
   ⍝. Then that unique combination is added to CellXFS table. The CellXfs index is the index used by the cell to assign a style
     xl←##.Main.XL

   ⍝. Single values for a given style must be enclosed as seen in the simpleStyle
     nfmt←⊂'#,##0'
     font←⊂20 1 'Calibri' 2
     fill←⊂'solid' 'F12345' '0000cc'
     brdr←⊂5⍴('thin' 1)('thick' '0000cc')
     simpleStyle←nfmt font fill brdr

  ⍝. All function results are of the following form
  ⍝. Check that the worksheet was has the correct style in the correct position
  ⍝. Also check that the cellXfs indices are set correctly
     ConfirmValues←{((1 4⍴⍺)≡¯1↑⍵.Styles.cellXfs)∧(((⍴4⊃sheet)⍴1)≡3⊃sheet←⊃↓⍵.WS)}

  ⍝. Before Each
     NewWB←{ ⍝ Return an empty workbook
         fileName←(739⌶0),'/XL2APLSimpleSheet.xlsx'
         overwrite←1
         xl.WB fileName overwrite
     }

     BasicStyle←{
         ⍝. Test adding a basic set of styles
         wb←NewWB''
         value←3 3⍴⍳9
         _←wb xl.WSAdd'TestSheetName' 'A1'simpleStyle value
         ((⍴value)⍴164 1 2 1)ConfirmValues wb
     }

     OnlyNumFmt←{
         ⍝. Test adding only number formats
         ⍝. Number format indices start at 164, because there are built-in numbers
         ⍝. APL2XL treats all numberformats as not being built-in
         wb←NewWB''
         style←(simpleStyle[0]),(3⍴⊂'')
         value←3 3⍴⍳9
         _←wb xl.WSAdd'TestSheetName' 'A1'style value
         ((⍴value)⍴164 0 0 0)ConfirmValues wb
     }

     OnlyFont←{
         ⍝. Test adding only a new font and nothing else
         ⍝. wb.Styles.fonts has a default font, and thus the new font is at index 1, and not 0
         wb←NewWB''
         value←3 3⍴⍳9
         style←(⊂''),(simpleStyle[1]),(2⍴⊂'')
         _←wb xl.WSAdd'TestSheetName' 'A1'style value
         ((⍴value)⍴0 1 0 0)ConfirmValues wb
     }

     OnlyFill←{
         ⍝. Test adding only a new fill and nothing else
         ⍝. wb.Styles.fonts has 2 default fills, and thus the new font is at index 2, and not 1
         wb←NewWB''
         value←3 3⍴⍳9
         style←(2⍴⊂''),(simpleStyle[2]),⊂''
         _←wb xl.WSAdd'TestSheetName' 'A1'style value
         ((⍴value)⍴0 0 2 0)ConfirmValues wb
     }

     OnlyBorder←{
         ⍝. Test adding only a new border and nothing else
         ⍝. wb.Styles.borders has a default border, and thus the new font is at index 1, and not 0
         wb←NewWB''
         value←(3 3⍴⍳9)
         style←(3⍴⊂''),simpleStyle[3]
         _←wb xl.WSAdd'TestSheetName' 'A1'style value
         ((⍴value)⍴0 0 0 1)ConfirmValues wb
     }

     ColumnStyles←{
         ⍝. Test adding a single style per column in a multi column range
         ⍝. Styles can be arranged column wise by making the length of each style the same as there are columns
         ⍝. In this case, 3 styles, 5 3⍴value, 3 columns
         numFmt←'#,##0' '0.00%' 'm/d/yyyy'
         fonts←(20 1 'Calibri' 2)(40 1 'Calibri' 2)(10 1 'Calibri' 2)
         fills←('solid' 'F12345' 'ABCDEF')('solid' 'AA1122' '0000cc')('solid' '0cc133' 'FF6699')
         borders←(5⍴⊂'thick' 1)(5⍴⊂'thin' '00FF00')(5⍴⊂'thin' 'FF00FF')
         value←5 3⍴10000+⍳10
         wb←NewWB''
         style←numFmt fonts fills borders
         _←wb xl.WSAdd'TestSheetName' 'A1'style value
         cellXfs←4 4⍴0 0 0 0 164 1 2 1 165 2 3 2 166 3 4 3
         (cellXfs≡wb.Styles.cellXfs)∧((⍴value)⍴1 2 3)≡3⊃⊃↓wb.WS
     }

     RowStyles←{
         ⍝. Test that it is possible to arrange styles per row
         ⍝. Row styles must be manually arranged unlike columns
         numFmt←'#,##0' '@' 'm/d/yyyy'
         fonts←(20 1 'Calibri' 2)(40 1 'Calibri' 2)(10 1 'Calibri' 2)
         fills←('solid' 'F12345' 'ABCDEF')('solid' 'AA1122' '0000cc')('solid' '0cc133' 'FF6699')
         borders←(5⍴⊂'thick' 1)(5⍴⊂'thin' '00FF00')(5⍴⊂'thin' 'FF00FF')
         value←3 5⍴10000+⍳10
         wb←NewWB''
         style←{⍉5 3⍴⍵}¨numFmt fonts fills borders ⍝ rearrange styles to be row oriented
         _←wb xl.WSAdd'TestSheetName' 'A1'style value
         cellXfs←4 4⍴0 0 0 0 164 1 2 1 165 2 3 2 166 3 4 3
         (cellXfs≡wb.Styles.cellXfs)∧(⍉(⌽⍴value)⍴1 2 3)≡3⊃⊃↓wb.WS
     }

     ⊃∧/(BasicStyle'')(OnlyNumFmt'')(OnlyFont'')(OnlyFill'')(OnlyBorder'')(ColumnStyles'')(RowStyles'')
 }
