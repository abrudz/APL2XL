 name←CompileXML wb;outputFile;header;ap;ax;cp;cx;ctp;ctx;rrp;rrx;rwp;rwx;ssp;ssx;wbp;wbx;wsp;wsx;paths;xmls;p;x;output;stylep;stylex;temp_excel_directory;step
⍝. wb is an instance of ##.XL.WB namespace containing all the collections required to populate a workbook
⍝. This Function calls all XML generating functions,
⍝. creates the directory structure required for a .xlsx file,
⍝. and zips the folder to create an Excel workbook

 name←wb.Name
 outputFile←∊¯1↓1 ⎕NPARTS wb.Name  ⍝ output file without extension, if any. correct extension added in Zip
 header←'<?xml version="1.0" encoding="UTF-8" standalone="yes"?>'
 temp_excel_directory←(739⌶0),'/temp_excel_directory'

 ⍝. Validation: If any error occurs, exit to the top of Main.Export
 :Trap 0
     step←'creating temp folder'
     CreateXLSXDirectory temp_excel_directory 

     :If wb.ShouldDelete
         step←'deleting existing file'
         _←3 ⎕NDELETE outputFile,'.xlsx'
     :EndIf

     step←'creating output file'
     ⎕MKDIR outputFile,'.xlsx'
     3 ⎕NDELETE outputFile,'.xlsx'
 :Else
     ('Error ',step,': ',⎕DMX.(Message,' ',,⍕OSError))⎕SIGNAL 777
 :EndTrap

 ⍝. The following code compiles the XML from each collection and generates the filename into which the XML is written
 ap←##.XL.AppPATH wb
 ax←##.XL.AppXML wb

 cp←##.XL.CorePATH wb
 cx←##.XL.CoreXML wb

 ctp←##.XL.CTPATH wb
 ctx←##.XL.CTXML wb

 rrp←##.XL.RRPATH wb
 rrx←##.XL.RRXML wb

 rwp←##.XL.RWPATH wb
 rwx←##.XL.RWXML wb

 ssp←##.XL.SSPATH wb
 ssx←##.XL.SSXML wb

 stylep←##.XL.StylePATH wb
 stylex←##.XL.StyleXML wb

 wbp←##.XL.WBPATH wb
 wbx←##.XL.WBXML wb

 wsp←##.XL.WSPATH wb
 wsx←##.XL.WSXML wb

 paths←ap cp ctp rrp rwp ssp stylep wbp wsp
 xmls←ax cx ctx rrx rwx ssx stylex wbx wsx

⍝. Combine the paths with the XML, and output XML to each path
 p x←{⊃,/⊆¨⍵}¨paths xmls
 outputXML←⍉↑p({header ⍵}¨x)

⍝. Write the output to the paths inside of the correct XLSX directory structure, and Zip to outputFile 
 temp_excel_directory WriteXML⍤1⊢outputXML
 temp_excel_directory Zip outputFile

⍝. This deletes a folder known to be created by this program. Safe to delete
 _←3 ⎕NDELETE temp_excel_directory
