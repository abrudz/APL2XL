 CompileXML wb;folder;header;ap;ax;cp;cx;ctp;ctx;rrp;rrx;rwp;rwx;ssp;ssx;wbp;wbx;wsp;wsx;paths;xmls;p;x;output;stylep;stylex
⍝. wb (right argument) is an instance of ##.XL.WB namespace containing all the collections required to populate a workbook
⍝. Style Disclaimer: This is a tradfn because it causes side-effects

⍝. This Function calls all XML generating functions
⍝. Then creates the directory structure required for a .xlsx file
⍝. Then zips the folder to create an Excel workbook
 name←wb.Name
 folder←∊¯1↓1 ⎕NPARTS⊃(0=≢name)⌷name'tempXLSX'
 header←'<?xml version="1.0" encoding="UTF-8" standalone="yes"?>'

 ⍝. The following code compiles the XML from each collection and generates the filename into which the XML is written
 ap←##.XL.AppPATH wb
 ax←##.XL.AppXML wb

 cp←##.XL.CorePATH wb
 cx←##.XL.CoreXML wb

 ctp←##.XL.CTPATH wb
 ctx←##.XL.CTXML wb

 rrp←##.XL.RRPATH wb
 rrx←##.XL.RRXML wb

 rwp←##.XL.RWPATH wb
 rwx←##.XL.RWXML wb

 ssp←##.XL.SSPATH wb
 ssx←##.XL.SSXML wb

 stylep←##.XL.StylePATH wb
 stylex←##.XL.StyleXML wb

 wbp←##.XL.WBPATH wb
 wbx←##.XL.WBXML wb

 wsp←##.XL.WSPATH wb
 wsx←##.XL.WSXML wb

 paths←ap cp ctp rrp rwp ssp stylep wbp wsp
 xmls←ax cx ctx rrx rwx ssx stylex wbx wsx

⍝. Combine the paths with the XML, and output XML to each path
 p x←{⊃,/⊆¨⍵}¨paths xmls
 output←⍉↑p({header ⍵}¨x)
⍝. Write the output to the paths inside of the correct XLSX directory structure
 output CreateXLSXDirectory folder
 Zip folder 1
