 CFSheetsXML←{
    ⍝ ⍵ ←→ vector ⎕NS
    ⍝ ⍵.CellRange ←→ 'A1:J100'
    ⍝ ⍵.Formula   ←→ 'ISODD(A1)'
     cf←{
         props←'type' 'dxfId' 'priority' 'stopIfTrue'
         vals←(⊂⍺),⍵.(DXFID Priority StopIfTrue)
         1 'cfRule' ''(props,⍪vals)
     }

     iconSet←{
         name  ←'iconSet'
         cfRule←name cf ⍵
         parent←2 name ''(1 2⍴name ⍵.IconSet)
         cfvo  ←{(3 'cfvo' ''),∘⊂⍤2⊢2 2⍴⍤1⊢('type' 'percent' 'val'),⍤1⊢⍪⌊(100÷⍵)×⍳⍵}⍎⊃⍵.IconSet
         cfRule⍪parent⍪cfvo
     }

     colorScale←{
         name  ←'colorScale'
         cfRule←name cf ⍵
         parent←2 name '' ''
         cvfo  ←↑{3 'cfvo' ''(((1+1≠≢⍵),2)⍴(⊂'type'),⍵)}¨(⊂'min')('percentile' 'val' 50)(⊂'max')
         colors←(3 'color' ''),⍤1⊢⍪{⊂1 2⍴⍵}⍤1⊢(⊂'rgb'),⍪⍵.ColorScale
         cfRule⍪parent⍪cvfo⍪colors
     }

     default←{
         props←'type' 'dxfId' 'priority' 'stopIfTrue'
         vars←(⊂'expression'),⍵.(DXFID Priority StopIfTrue)
         c←1 'cfRule' ''(props,⍪vars)
         f←1 4⍴2 'formula'⍵.Formula(0 2⍴⊂'')
         c⍪f
     }


     ⍵≡⊂'':⍵
     formats←⍵
     priorities←⍵.Priority
     _←⍺ CFAdd formats
     group←{⍺,⊂formats[⍵]}⌸formats.CellRange

     xml←{
         (sqref cfs)←⍵
         results←{
             0≠⍵.⎕NC'ColorScale':colorScale ⍵
             0≠⍵.⎕NC'IconSet':iconSet ⍵

             default ⍵
         }¨cfs
         p←0 'conditionalFormatting' '',⊂1 2⍴'sqref'sqref    ⍝ parent
         ⎕XML p⍪⊃⍪/results
     }
     ⊂∊xml⍤1⊢group

 }
