 WSProps←{
     ⍝. ⍵ ←→ sheet
     ⍝. ⍺ ←→ internal wb data structure
     ⍝ Handle all sheet global collections and properties
     wb←⍺
     sheet←⍵
     freezePane←{
         ⍝. Take a worksheet namespace, and return the pane properties for a freezepane
         0≥⍵.⎕NC'FreezePane':⍬
         (count orientation)←⍵⍎'FreezePane'
         c←⍕count
         o←'rows' 'columns'⍳⊂orientation
         pane←o⊃'bottomLeft' 'topRight'
         split←o⊃'ySplit' 'xSplit'
         tlcell←itc o⊃(count,0)(0,count)
         range←o⊃(('XFD',¯1↑tlcell)((⊃tlcell),'1048574'))

         pattrs←4 2⍴split c'topLeftCell'tlcell'activePane'pane'state' 'frozen'
         sattrs←3 2⍴'pane'pane'activeCell'tlcell'sqref'(tlcell,':',range)
         (2 'pane' ''pattrs)(2 'selection' ''sattrs)
     }sheet

     defaultSelection←1 'selection' ''('sqref' 'A1:XFD1048576')

     wbv←'workbookViewId' '0'
     attrs←(('tabSelected' '1')wbv)
     svs←(⎕XML↑(0 'sheetViews' ''(0 2⍴''))defaultSelection(1 'sheetView' ''(↑attrs)),freezePane)

     collection←{n w←⍵ ⋄ 1 'col' '',⊂↑('min'(⍕n))('max'(⍕n))('width'(⍕w))('bestFit' '1')('customWidth' '1')}⍤1⊢sheet.ColumnWidth
     columns←⎕XML(0 'cols' '' '')⍪collection

     ⍺.SheetViews⍪←⍵.Name(svs,'<sheetFormatPr defaultRowHeight="14.5" x14ac:dyDescent="0.35"/>',columns)
     wb
 }
