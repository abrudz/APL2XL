 WS_convert←{
     ⍝. ⍵ ←→ must be a simple vector or table (unless a list of nested char vecs)
     ⍝. ⍺ ←→ wokbook namespace
     ⍝. Convert apl datatyes to Excel recognized datatypes
     ⍝. Returns 2 element vector: Values TypeList
     ⍝⍝⍝ TODO(nathan): This function is very naive and primitive. Other type checking may be necessary
     gett←{drs∊⍨r←⎕DR ⍵:r ⋄ ⎕DR⊃⍵} ⍝ get the data type of a simple homogenous array
     mapt←'b' 's' 'n' 's' 'default' ⍝ Excel types
     drs←11 80 83 160

     ⍝. Define data conversion functions named by mapt types
     b←{⍵:'True' ⋄ 'False'} ⍝ boolean conversion function 
     n←{⍵}                  ⍝ numerical conversion function
     s←⍺∘SSAdd              ⍝ string conversion function
     default←n              ⍝ default function is numerical

     ⍝. Convert the list of data to a list of types
     i←drs⍳gett ⍵              ⍝ get the xlsx type
     t←mapt⌷⍨i                 ⍝ the xlsx type string
     v←{(⍎∘{1=≡⍵:⍵ ⋄ ⊃⍵}t)⍵}¨⍵ ⍝ convert to xlbased on type
     v t
 }
